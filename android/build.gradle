apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = "org.lovebing.reactnative"
archivesBaseName = "baidumap"
version = "1.0.1"

buildscript {
    repositories {
        mavenLocal()
        google()
        jcenter()
        maven() {
            url '/usr/local/node/lib/node_modules/react-native/android'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
    }
}

repositories {
    mavenLocal()
    google()
    jcenter()
    maven {
        url '/usr/local/node/lib/node_modules/react-native/android'
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.0'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                name 'BaidMap for React Native'
                packaging 'jar'
                description 'BaidMap for React Native'
                url 'https://github.com/lovebing/react-native-baidu-map/tree/master/android'

                scm {
                    connection 'scm:https://github.com/lovebing/react-native-baidu-map.git'
                    developerConnection 'scm:https://github.com/lovebing/react-native-baidu-map.git'
                    url 'https://github.com/lovebing/react-native-baidu-map.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://mit-license.org/'
                    }
                }

                developers {
                    developer {
                        id 'lovebing'
                        name 'Tang Yangjian'
                        email 'tangyangjian@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    compileOnly 'com.facebook.react:react-native:+'
    implementation files('libs/BaiduLBS_Android.jar')
    provided files('src/main/assets')
}

